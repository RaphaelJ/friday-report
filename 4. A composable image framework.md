# A composable image framework

This section will present two cool features of *friday*. As I said in the
introduction, the library has the property that you can compose algorithms and
expect them to be *fused in a single and efficient loop* or *automatically
parallelized*.

The main motivation when designing this feature was to be able to design complex
but efficient transformations from a small set of simple functions (like `map`).

## The image and function isomorphism

To understand how the compiler and the library are allowed to fuse and
parallelize algorithms, one must remember *referential transparency*.
Referential transparency is the property which asserts that *pure function
calls can be replaced by the values they generate without changing the output
of the program*.

This property has an interesting effect on images. Images (and more generally
arrays and vectors), in a *pure* functional programming context, may be thought 
of as functions whose domains are restricted to a contiguous subset of integers
(the valid coordinates of the pixels in the case of an image). In other words,
you can replace the image indexing operator by the function which generates the
pixels of the image *without* changing the output of the program.

The main advantage of considering images as functions instead of arrays is
that functions are composable. Function composability is the idea which propels
the fusion mechanism: *friday considers images as functions and composes them
into larger functions*.

## A low level image creation interface

Image types implement a type class interface named `FromFunction` which can be 
used to build images.

*The type class provides a function to construct an image instance from a pure
function*. The function's signature looks like this one:

```Haskell
fromFunction :: Size -> (Point -> ImagePixel i) -> i
```

The function accepts the size of the image to create and a pure function which
generates pixels from their coordinates.

You usually don't need to use `fromFunction` directly. Functions like `map`
provide an higher level interface and are implemented using `fromFunction`.

## Two image representations

Two image types implement the previously described `FromFunction` type class.
One is named the **manifest representation** while the other is named the
**delayed representation**.

* The *manifest* representation uses an in memory vector to store the pixels
  values. The vector is entirely initialized when this image is constructed
  with `fromFunction`.
* The *delayed* representation only stores the size and the function which
  constructs the image.

In other words, manifest images are memory resident images while delayed images
are only composed of the function that generate pixel values.

Both image types also implement the `Image` type class which provides an
interface to get pixel values. They can therefore be freely substituted as you
usually only access to these two using this interface.

## Fusing delayed images

Knowing about the manifest and the delayed representations, we can now write a
pipeline of transformations and only have the original and the resulting image
to exist in memory.

Let's write a program that creates a black and white thumbnail by

1. converting the image to grey-scale.
2. applying our `blackAndWhite` function to convert grey-scale pixels to black
   and white values. We defined the `blackAndWhite` function in the course of 
   the previous section using the `map` function.
3. removing edges so the image is a square as large as its smallest side.
4. resizing the resulting image to be 250 pixels wide.

Here is the source code of the program which created this thumbnail:

```Haskell
-- Gets the size of the input image.
(h, w)  = shape input

-- Gets a grey scale delayed image from the input image.
grey    = delayed (convert input)

-- Applies our `blackAndWhite` function to the grey-scale image.
black   = delayed (blackAndWhite grey)

-- Creates a rectangle (Rect) object which will be used to define how we
-- will crop/cut our image and remove edges.
--
-- The rectangle is centered on the largest side of the image.
rect    = if w > h then Rect ((w - h) / 2) 0 h h
                        else Rect 0 ((h - w) / 2) w w

-- Crops the image.
cropped = delayed (crop rect black)

-- Resizes the image.
--
-- By using the delayed representations, our compiler should be able to
-- fuse these four transformations into a single loop.
resized = manifest (resize Bilinear (250, 250) cropped)
```

Notice the use of the two `manifest` and `delayed` functions. These two do
nothing except giving hints to the compiler on what image representation we
want to generate. If we don't write these functions, the *Haskell*'s compiler
will fail and complain that it can't unambiguously determine what image types to
generate.

## Implementation

The *Haskell*'s compiler will generate an efficient loop from the previous
program.

Functions which accept delayed images or which return delayed images are
requested to be inlined. In the case of the previous example, `convert` will be
inlined in `blackAndWhite`, which will be inlined in `crop`, which will be
inlined in `resize`, which will be inlined.

The `fromFunction` call inside the `resize` function will also be inlined. As
the `fromFunction` implementation to generate manifest images is a loop which 
calls the pixel generation function for the entire image, this will result in a
specialized loop body incorporating all the inlined pipeline.

It's quite difficult to figure out why this works if you're not used to this
kind of pattern (we ask the compiler to inline functions which receive inline
functions as arguments). But trust me, it does work.

If an algorithm is called with manifest images (like in other image processing
libraries), it will not be inlined. The compiler will however be requested to create a specialized instance for the requested pixel type(s).

## Automatic parallelization

The library also provide an useful function named `computeP`, for "*compute* in
*p*arallel".

The function is actually really simple: it computes a manifest image from 
a delayed image. But it uses available processors to share the work.

Say that we want to parallelize the thumbnail program we just wrote, we just
need to replace this line:

    resized = manifest (resize Bilinear (250, 250) cropped)

By this line:

    resized = computeP (resize Bilinear (250, 250) cropped)

... and the compiler will generate a parallel loop for our entire pipeline of
transformations !

We will not notice an huge speedup because of the small size of the thumbnails
we generate (250 x 250 pixels), but it gives an almost linear speedup on
programs that work on larger images.

You can freely substitute calls to `manifest` for calls to `computeP` without 
risking any race condition as *pure functions are always thread safe*.


## Mutable images

*There is some image processing algorithms that can't be expressed efficiently without mutable states* (i.e. they can't be efficiently written with
`fromFunction`).

Drawing routines (e.g. drawing a rectangle in an image or the flood fill
algorithm) are such algorithms.

For this reason, *manifest images can also be created in a context where mutable
states are allowed*. When in this context, the programmer is able to update
image values exactly as he would do in an imperative programming language.

Functions written in this context are still deterministic as you can't do side
effect nor use uninitialized variables. A mutable state is also not allowed to
leak outside this context (the context must return immutable values). Those
functions appear *pure* from the outside. We rely on the type checker to
enforce this closed containment, by using a functional pattern named *monads*.
Monads are a far too advanced concept to be introduced in this document.

Delayed images can't be created in this mutable context. Thus *image
transformations written this way can't be fused nor automatically parallelized*.

## Performances

The library has been reasonably optimized and could be used to a limited extent
for real-time applications.

The graph in Appendix shows how the library compares to the *OpenCV* and the
*ImageMagick* libraries, both written in C. Multi-core parallelism was
*disabled* on all three libraries. The fastest implementation for each 
algorithm is taken as reference.

*friday* is about one order of magnitude slower than *OpenCV* but does well
against *ImageMagick*.

*OpenCV* performances are explained by hand-written specialized algorithms which
make an intensive use of vectorial instructions (SSE, AVX and the like) for
common image types.
